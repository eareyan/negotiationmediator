;
;;;
;;; enrique's scenario generator
;;;
;
#|

To create a scenario, run the following function in the lisp interpreter:

(enrique :numissues 1
         :domainsize 100
         :numconstraints 5
         :dimprobs '((1 100)) 
         :widthprobs '((1 100)))

(enrique :numissues 1
         :domainsize 10
         :numconstraints 50
         :dimprobs '((1 100)) 
         :widthprobs '((1 100)))

(enrique :numissues 2
         :domainsize 10
         :numconstraints 5
         :dimprobs '((2 50) (1 50)) 
         :widthprobs '((1 20) (2 20) (3 20) (4 20) (5 20)))

(enrique :numissues 3
         :domainsize 10
         :numconstraints 5
         :dimprobs '((1 50) (2 25) (3 25)) 
         :widthprobs '((1 20) (2 20) (3 20) (4 20) (5 20)))

numissues = number of issues in domain
domainsize = number of values for each issue 0 .. <domainsize - 1>
numconstraints = number of hypercubes
dimprobs = probability distribution for number of dimensions per hypervolume
widthprobs = probability distribution for width of a hypervolume on a given issue 

In the example above, there are:

   two issues whose value can range from 0 .. 19
   20 hypervolumes
   each hypervolume is equally likely to have one or two issues
   the hypervolumes range from 1 to 5 wide on each dimension, with equal probability

|#

(setq *random-state* (make-random-state t))

(defun gav (entity attribute) 
  "Gets the attribute value for an entity"
  (getf (rest entity) attribute))

(defun sav (entity attribute value)
  "Sets the attribute value for an entity"
  (setf (getf (rest entity) attribute) value))

(defsetf gav sav)

(defun compress (number)
  "Compresses positive integers to base 36"
    (format nil "窿蝈鲥蝮祜镳鏖翳溟玳骘轭溴骝镯祜铛礅弪扯滹黝麸滹眭祠轲戾鲠祯瀛忾钿颟趄躅汜翦铛礅弪扯箦赳溟玳颟箦赳铛礅弪愆泔祆邈篚怏羼氨渤吹斗腹谅媚牌侨墒颂臀闲岩釉罩棕仝溟玳ū溟玳舂┅┅ㄤ彐疳蜥礤翦箦篌轱瞽殇ㄣ镯痱弩ㄧ弭躅轹弪筢飙糸礤┅ㄤ彐躅蝈徜麸沆ㄡ蜱⑸铞镫弩蝈徜骝镯篝蜷铉轭泔眄镱扉箴躞弪疳汶徵濯戾è疳汶徵濯ㄦ轭洵疳汶徵с镯盹瞽扉箴躞弪┅麒孱篝蜷铉狎绌蝈徜骝镯篝蜷铉狎铋铋飑┅ㄤ彐躅铄鳝殇é镳糸镱犰簌礅镬螬⒚蝈狒弩铄殇戾舄è簌礅镬蝈徜麸沆ㄦ矧磲铋岘幄簌礅镬箦篌轱瞽殇┅ㄩ钿屮矧ㄧ弭簌礅镬ь鬻簌憝泔躅舂癌┅箦翩ㄧ弭簌礅镬ь鬻簌憝泔躅舂ū轭溴┅ㄦ矧磲铋岘幄簌礅镬ū轭溴┅┅ㄤ彐躅筱蜥礅戾扉篝⒂泸犴忪弩翳矧溴镦扉篝箫螋ㄣ镳扉篝扉篝＇灬礅溽ㄡ猢ㄤ邈灬蝈ㄩ珙矧猢弪镳蜥钿镯博┅┅ㄤ彐躅疳轵扉篝⒁弭躜铙扉篝镦犰疳轵镦轸屙轭翳扉篝祜镳骘骝镯麸戾铉翳扉篝钽镱祜镳骘骝镯ū椹麸戾铉翳扉篝骘霰铘ū椹扉篝骘霾铘ū戛扉篝泔祆邈扉篝霰霾┅┅ㄤ彐躅犰飙轶篚弩ㄤ镯衢瞟⒁弭躜铙翳轶篚弩轭滹磲轭祜镳骘屐屙孱轭滹磲轭狃疱钿ㄣ狍ㄦ轵篝屐屙孱舂ㄩ篌蹂扉篝屐屙孱舂ㄣ镯痫铄铘ㄡ痧孱ㄧ狯屐屙孱洪篌蹂螬ㄡ祆轶篚弩ㄧ狯屐屙孱恒镯痫铄铘螬┅┅┅ㄤ彐躅痖汶骝镯扉篝扉篝⑿殂塍蜥钿镯轸屙骝镯玳鲥扉篝麒孱扉篝铘蜥钿镯戾铉翳扉篝┅扉篝┅ㄤ彐躅痖汶骝镯蜥铉黹磲⑿殂塍蜥钿镯轭翦珏骝镯玳鲥蜥铉澧ǐ黹蜥钿镯ō磲黹暴┅ㄤ彐躅痖汶麇殓梏邃痱镡螬⑿殂塍蜥钿镯汨镩沐骝镯痱镡徕殪轸岘扉篝麒孱痱镡祜镳鏖翳箝祜镳骘轭痱镡篚箦泔钿皓鏖翳翳蝈箬镬痖汶骝镯蜥铉箝濠鏖翳篚骘痱镡轭痱镡滹箦赳篚ǐ篚箦泔钿痱镡┅滹麒孱窘篚翳蝈箬镬洎蝈趱蝾ㄦ轵篝痱镡┅┅┅ㄤ彐躅痖汶扉篝⒁犷滹盱痖汶轸屙骝镯扉篝祜镳蝈疱狒骘轭筱蜥礅戾扉篝泔祆邈悌ㄤ彐躅蝈铛礅弪ㄤ镯衢瞟⑽蹴忮蝮翳轶篚弩轭滹磲轭轭翳矧溴翳妁狃疱狎戾è轭溴癌灬忮祗è蝈铛ㄥ戾愆ㄣ狍ㄦ轵篝屐屙ㄩ篌蹂箦翩ㄧ狯屐屙洪钿屮ㄩ钽轭溴┅屐屙ㄣ镯痫铄铘啜泔眇镱孱侯犴ㄧ狯屐屙侯犴濠洪篌蹂祜镳骘轭ㄧ狯屐屙洪篌蹂螬泔祆邈蝈铛椹恒镯痫铄铘祜镳骘铄轭ㄧ狯屐屙恒镯痫铄铘螬泔祆邈蝈铛铄舂┅┅┅祜镳骘轭滹磲轭泔祆邈蝈铛悌┅┅ㄤ彐躅泸遽翦筱孱狎轱é脲钺礤⑨筱孱狎轱ㄤ弩泸轲糸镱铋飑铛磲珏铘博铛磴镯痫铄铘暴铛黹篌蹂嘲ㄤ镯衢铙辁卑铛磴镱篝蜥轭趔卑铛眢栳蝈癌麇殓梏忾狍爱订翎珞铋飑ㄣ痱彐癌ㄡ痱彐癌豉疱痱镡Жㄣ踱吵ㄢ屐吵痨犷吵铒吵┅ㄤ轫痱镡Жū吵ú吵ǔ吵ù吵┅筱镳屦蝻怏Жㄣ镯痫铄铘吵箝忪轭吵ㄣ栝熹吵┅鏖漪桊蝻怏祜镳骘骝镯麸滹磲轭箝泔祆邈扉篝卑┅箬狃邂獒癌扉篝狃蝈沭蝈姗⒚蝈狒弩筱孱狎轱骈戾鏖翳栝弪狎汨殂犰豸殪轸骢钽糸镱钺礤钺礤镦翳筱孱狎轱铛磴镯痫铄铘镦泔眇镱孱趔铛黹篌蹂镦轶篚弩铛磲珏铘镦徵孱趔滹磲轭箝箝镦滹磲轭骘遽汨轶篚铛磴镱篝蜥轭趔镦泔铙趄衢铘轭遽汨徵孱趔豸殪轸骢钽糸镱铛眢栳蝈镦泔铙趄衢铘翳狒狎箬狎邃秭弪犰翳徵孱趔涉翳轶铄玑糸鲥翳箬狎邃泔铙趄衢铘狎轭鲥螋邃徙蝻篌徵孱趔麇殓梏忾狍栾聃殂腱泔铙趄衢铘麇殓梏溴泸遽箦鏖翳溴痿溟眇蝻怏翳痱镡徕扉扉豉镦溟骀弪孱泔铙趄衢铘溟礤铙轱铙筱镳屦蝻怏痱镡徕殪轸镦溟骀弪孱筱镳弩镦泔铙趄衢铘筢礤泔眇镱孱衄箝忪轭绗汨殪洎豉疱痱镡痱镡徕殪轸骘遽汨豉疱镦泔铙趄衢铘ㄣ踱瀣忮祆痨犷濠鏖漪桊蝻怏痱镡徕殪轸骘溟骀弪孱痫篌殁戾泔铘蜥轭鏖漪梵箬狃邂獒忾狍麸麽蜾鏖溴趄邋痫箝糸鲥鲠祯濠矧钺蝌秣趄邋铄玑糸鲥鲠祯濠戾舄è犰飙轶篚弩铋飑ㄡ祆泔眇镱孱趔铋飑疳蜥礤翦蝮啜侯蹴徵孱趔铛磲珏铘侯蹴泔眇镱孱趔铛磴镯痫铄铘侯蹴轶篚弩铛黹篌蹂轰镯衢铙辁滹磲轭箝侯蹴泔铙趄衢铘铛磴镱篝蜥轭趔侯蹴箬狎邃铛眢栳蝈瑚彘玷翕獒麇殓梏忾狍呼疱痱镡豉疱痱镡轰轫痱镡溟眇蝻怏后泔疱痱镡筱镳屦蝻怏瑚殇翳痱镡鏖漪桊蝻怏后栳疱忾狍箬狃邂獒螬┅灬忮祗è疳翳ㄥ铘轸⑵轭潴翳疳翳骘犷孱糸豉ㄧ狯孱糸豉吼狒瑭ㄦ轭洵泔眇镱孱钺礤⑵轭潴翳泔眇镱孱鏖翳翳玳鲥钺礤ㄦ轭钺礤犰飙泔眇镱孱趔呼弩＇羼踽弘妁＇灬礅溽ㄣㄧ狯侯犴濠┅疳蝈铘ㄣ镯痫铄铘⑵轭潴翳疳蝈铘骘翳玳鲥泔眇镱孱簪ㄦ轭洵泔眇镱孱ㄦ轵篝疳翳泔眇镱孱舂┅箝忪轭珞ㄣ镯痫铄铘⒁弭躜铙翳箝忪轭珞骘泔眇镱孱簪ㄧ狯疳蝈铘泔眇镱孱舂恒镯痫铄铘螬ㄤ屦翳ㄥ⑵轭潴翳溴痿镦犷孱糸豉轭翳泔眇镱孱栝弪狎汨戾铉翳疳翳濠┅ㄦ轭洵轶篚ㄩ钿屮⑵轭潴轶篚鏖翳翳玳鲥轭溴ㄦ轭轭溴犰飙轶篚弩弘妁＇灬礅溽ㄩ篌蹂ㄧ狯轶篚洪钿屮┅┅ㄡ滗轶篚ㄣ⒘滗铄轶篚麸翳泔眇镱孱簪戾舄è泐犴ㄧ狯侯犴濠ㄩ钺礤ㄦ矧磲铋岘辋幄泐犴ū戾铉翳ㄧ狯洪篌蹂螬┅┅ㄩ篌蹂啜轶篚呼疱轭翦珏侯犴轭犴红弼屐ū戾铉翳ㄧ狯吼狒瑭┅烘蝻呼ū滹磲轭箝濠┅瘐箬轶篚犰飙轶篚弩瘐箬轶篚ㄧ狯洪篌蹂螬┅ㄣ蝈狒瀛滹磲轭ī⒚蝈狒弩滹磲轭溴骈铋糸镱祜镳骘泐蹴骝镯麸铛磴镯痫铄铘骘泐犴ㄦ矧磲铋泾幄泐蹴骘疳蝈铘痖汶麇殓梏邃祜镳骘轭犰飙泔眇镱孱趔骘铍ū戾铉翳ㄧ狯恒镯痫铄铘螬┅骘麇殓梏趄躅汜翦í卑ㄥ痿铍箬狃邂獒螬┅泔祆邈扉篝麇殓梏┅骘疳翳麒孱疳蝈铘ㄣ镱ㄧ狯疳蝈铘侯犴濠ㄧ狯疳蝈铘吼狒瑭┅骘铄啜泔眇镱孱侯犴泐犴吼狒疳翳滹瘐箬铄犰飙泔眇镱孱趔滹麒孱疳蝈铘瘐箬铄ㄧ狯疳蝈铘恒镯痫铄铘螬┅祜镳骘轭犰飙泔眇镱孱趔滹ㄡ滗轶篚悌祜镳蝈疱狒ō铛黹篌蹂铛磴镯痫铄铘螬滹ㄡ滗轶篚痖汶骝镯扉篝犰飙泔眇镱孱趔┅蝈铛礅弪灬篝犰飙泔眇镱孱趔┅ㄣ蝈狒瀛筱犰瀛骝邋泔铙趄衢铘铛磴镱篝蜥轭趔⒄箦痱彐弪孱糸犰狒翎汨礤铘麸珏铄蜥翦筱犰瀛骝邋轶篚铄赭矧擘戾è泔铙趄衢铘祜镳骘骝镯麸铛磴镱篝蜥轭趔泔祆邈啜洪洪篌蹂铋飑┅ㄩ篌蹂祜镳骘骝镯麸铛黹篌蹂泔祆邈啜洪恒镱篝蜥轭趔铋飑┅祜镳骘轭泔铙趄衢铘骘钿轫痖汶麇殓梏邃溟眇蝻怏泔祆邈祜镳蝈疱狒钿轫骘麇殓梏邃祜镳骘轭轶篚弩泔祆邈扉篝ㄧ弭洪洎ū戾铉翳ㄧ弭洪篌蹂螬┅┅骘轶篚痖汶麇殓梏邃麇殓梏邃滹瘐箬铄ㄧ弭轶篚洪洎ㄧ弭洪篌蹂螬滹瘐箬铄ㄧ弭洪钿屮ㄧ弭轶篚恒镱篝蜥轭趔┅┅┅ㄣ蝈狒瀛泔铙趄衢铘铛磴镱篝蜥轭趔祜镳鏖翳泔铙趄衢铘麒殪戾铉翳泔铙趄衢铘螬铛磴镱篝蜥轭趔骘泔眇镱孱痖汶骝镯扉篝犰飙泔眇镱孱趔骘溟礤铙轱痖汶麇殓梏邃溟眇蝻怏骘轶篚弩ㄣ狍痖汶麇殓梏邃筱镳屦蝻怏ㄣ镯痫铄铘痖汶溟礤铙轱ㄧ狯泔眇镱孱洪篌蹂螬┅箝忪轭祜镳骘箝轭痖汶溟礤铙轱箝忪轭珞泔眇镱孱舂泔祆邈痖汶骝镯扉篝ㄧ狯箝洪篌蹂螬┅ㄣ栝熹戾è汨殪痖汶骝镯扉篝ㄧ狯泔眇镱孱恒镯痫铄铘螬┅麒孱汨殪扉篝痖汶骝镯扉篝ㄧ狯泔眇镱孱洪篌蹂螬痖汶骝镯扉篝ㄧ狯汨殪洪篌蹂螬┅┅┅滹膂恒镯痫铄铘泔眇镱孱轰轫孱箝镱溟礤铙轱洪篌蹂轶篚弩滹戾舄è怙躅潴祜镳骘溴轭轶篚弩骘轭溴ㄧ狯溴洪钿屮骘鏖漪痖汶麇殓梏邃鏖漪桊蝻怏骘黹ㄧ狯溴烘蝻愆骘磲ㄧ狯溴呼铹骘祓矬痖汶骝镯蜥铉ō黹鏖漪瑭磲骘戾骠磲黹祓矬骘蜷玷黹磲ǐ祓矬鏖漪瑭泔祆邈啜轭溴烘蝻戾骠呼蜷玷舂┅麇殓梏í痖汶骝镯蜥铉卑癌ㄥ痿麇殓梏忾狍ū祜镳骘轭轶篚弩磲轫辁ㄧ狯红弼屐┅┅┅箦赳怙躅潴箫螋怙躅潴＇弘妁＇骈蝮舂ㄣ狍痖汶麇殓梏邃豉疱痱镡螬ㄢ屐瘐箬啜忮祆怙躅潴麇殓梏泔铙趄衢铘螬ㄣ踱瘐箬啜沲忮怙躅潴麇殓梏泔铙趄衢铘螬痨犷瘐箬啜痨犷怙躅潴麇殓梏祜镳蝈疱狒戾铉翳怙躅潴泔祆邈痖汶骝镯蜥铉伯伯癌┅泔铙趄衢铘螬┅骈钺祆蝈趱蝾泔铙趄衢铘螬┅ㄣ镱è犷铛眢栳蝈癌铒ㄡ篌镢с踱豉疱痱镡螬铒ㄡ篌镢ь雉豉疱痱镡螬┅ㄥ蝌矧⒃珏镳痫箦泔铙趄衢铘蟋徵孱趔眭篝栳鲥沲忮矧铒泔铙趄衢铘螈┅èō铛眢栳蝈洎铛磴镱篝蜥轭趔ㄥ蝌矧①秕眭篝栳鲥狒戾狍镱蝈珲灬泔铙趄衢铘骘弼弪镳痫箦泔铙趄衢铘┅戾舄è滹磲轭ㄣ蝈狒瀛滹磲轭┅啜筱孱狎轱洪篝蜷铉铄鳝殇┅侯犴钺礤呼徵翎珞轰弩泸轲糸镱铋轰镯衢滹磲轭横珏铘祜镳骘骝镯麸铛磲珏铘泔祆邈啜徵孱洪ㄦ矧磲铋⒘幄椹乎骢ㄣ蝈狒瀛泔铙趄衢铘铛磴镱篝蜥轭趔┅鲤狎犴弭弪螬┅ㄣ镱è铛眢栳蝈癌祜镳骘疳轵轭疳轵ㄧ狯横珏铘螬骘箫躜沐ㄦ轵篝疳轵骘翎蜱弭箦泔钿疳轵滹祜镳鏖翳泔躅骘矧殓轭犰轭ㄧ狯箫躜沐乎骢瞟骘蝈鲥蝮ㄣ狍ㄦ轵篝矧殓轭犰ㄣ踱啜铒括蝈篝矧殓轭犰┅铒啜沲忮括蝈篝矧殓轭犰┅┅麒殪泔躅ō铛眢栳蝈洎滹麒孱蝈鲥蝮瘐箬蝈鲥蝮ㄧ狯翎蜱弭乎骢瞟ㄩ钽泔躅舂┅┅è铛眢栳蝈癌祜镳鏖翳箬狎邃ㄣ蝈狒瀛泔铙趄衢铘铛眢栳蝈洎骘徵孱轭ㄧ狯横珏铘螬滹祜镳骘轭箬狎邃滹瘐箬ㄧ狯徵孱乎骢瞟┅┅箦翩ㄧ狯轰弩泸轲糸镱鏖翳秕麴豸麸篝蜷铉篝蝈犴麒孱溴筱蜷痿轱ㄦ矧磲篝蝈犴⒓饩腻筱蜷痿轱罴饩良怛劲溴筱蜷痿轱瞟┅螬┅┅ㄤ彐躅屮痫螋铄顼盱筱孱狎轱骈戾⑴痫螋筱孱狎轱轭翳铄顼盱骘蝽狒鏖翳镳孱骈戾篝蝈犴骈戾轰轵邈糸镱猴豸瘐洪姝滹弩铒舡屮轶恒蝈狒洪姝屮轶趔后躔弪箦溴戾舄è滹磲轭ㄧ狯筱孱狎轱轰镯衢瞟ㄩ篌蹂ㄡ祆轶篚弩滹磲轭┅灬忮祗è箴徙弪戾鲥飑ㄦ矧磲铋狺祜镳蝈疱狒í戾鲥飑泔祆邈┅┅ㄦ矧磲篝蝈犴⒓筱孱狎轱殇杰彳⒕ㄧ狯筱孱狎轱洪洎ㄦ矧磲篝蝈犴ゼ滹磲轭劲祜镳骘屐屙轭轶篚弩滹ㄦ矧磲篝蝈犴峒轶篚豉疱杰㈤铘彗弪堍钺礤杰彳祜麇蜮秕钿杰彳躔疱蜮秕钿杰彳劲箴徙弪暴ㄧ狯屐屙洪钿屮ㄧ狯屐屙烘蝻愆ㄧ狯屐屙呼铹┅ㄦ矧磲篝蝈犴ゼ滹磲轭劲祜镳骘徵孱轭ㄧ狯筱孱狎轱横珏铘螬滹戾īㄦ矧磲篝蝈犴ゼ痱镦殪殇杰彳劲ㄧ狯徵孱洪洎ㄦ矧磲篝蝈犴峒蹑躅芫箴徙弪暴祜镳骘轭ㄧ狯徵孱乎骢瞟骘豉疱ㄦ轵篝悌骘怙躅潴箦泔钿悌骘麇殓梏翳轵悌滹戾īㄦ矧磲篝蝈犴峒桢殓梏杰彳⒕箴徙弪博豉疱麇殓梏祜镳骘骝镯骘怙躅轭怙躅潴骘轭溴ㄦ轵篝怙躅洎骘黹ㄧ狯怙躅烘蝻愆骘磲ㄧ狯怙躅呼铹骘箪镳铘ㄦ秕螋悌滹ㄣ狍豉疱痨犷ㄦ矧磲篝蝈犴峒怙躅轶篚褰堍彳黹罱堍彳磲杰彳箪镳褰堍彳劲箴徙弪畅轭溴黹磲箪镳濠ㄦ矧磲篝蝈犴峒怙躅轶篚褰堍彳黹罱堍彳磲杰彳劲箴徙弪畅轭溴黹磲┅┅ㄦ矧磲篝蝈犴峒峋箴徙弪博豉疱┅ㄦ矧磲篝蝈犴峒蹑躅劲箴徙弪暴ㄦ矧磲篝蝈犴ゼ痱镦殪寰┅ㄦ矧磲篝蝈犴ゼ筱孱狎轱峻ア┅┅ㄤ彐躅孱蜷聃é脲铛黹篌蹂博ㄤ镯衢铙辁卑铛磴镱篝蜥轭趔舶ㄤ轫痱镡Жú蛋ū蛋┅鏖漪桊蝻怏Жū舶ú舶ǔ舶ù舶ǖ舶┅┅戾舄èㄣ蝈狒瀛筱孱狎轱侯蹴徵孱趔侯蹴泔眇镱孱趔侯蹴轶篚弩铛黹篌蹂轰镯衢铙辁滹磲轭箝侯蹴泔铙趄衢铘铛磴镱篝蜥轭趔轰轫痱镡溟眇蝻怏瑚殇翳痱镡鏖漪桊蝻怏后泔疱痱镡Жㄣ镯痫铄铘卑癌呼疱痱镡Жㄣ踱吵┅┅ㄥ痫螋铄顼盱ㄦ矧磲铋狺岙盱ㄣ镱汜翦钺翦篝蜷铉阵弪蟑孱蜷聃遽蝈犷娘沲礤铘蟑黠螂箴徙瀵忉弩獒铐邃獒麸虔筱孱狎轱螈铛磉轶篚弩撷黩轸瀛麸篝蜷铉铛黹篌蹂螬滹磲轭唧辁暹黩轸瀛麸篝蜷铉滹磲轭箝濠铛磉泔铙趄衢铘筮黩轸瀛麸篝蜷铉铛磴镱篝蜥轭趔ㄧ狯洪洎┅痱轭ㄧ狯洪洎┅聃轸┅